unit U_DataModule;

interface

uses
  System.SysUtils, System.Classes, FireDAC.Phys.MySQLDef, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Error, FireDAC.UI.Intf, FireDAC.Phys.Intf,
  FireDAC.Stan.Def, FireDAC.Stan.Pool, FireDAC.Stan.Async, FireDAC.Phys,
  FireDAC.VCLUI.Wait, FireDAC.Comp.UI, Data.DB, FireDAC.Comp.Client,
  FireDAC.Phys.MySQL, FireDAC.Phys.MSAccDef, FireDAC.Phys.ODBCBase,
  FireDAC.Phys.MSAcc, FireDAC.Stan.Param, FireDAC.DatS, FireDAC.DApt.Intf,
  FireDAC.DApt, FireDAC.Comp.DataSet;

type
  TDataModule1 = class(TDataModule)
    FDConnection: TFDConnection;
    FDGUIxWaitCursor1: TFDGUIxWaitCursor;
    FDPhysMSAccessDriverLink1: TFDPhysMSAccessDriverLink;
    qryCardClientes: TFDQuery;
    qryClientes: TFDQuery;
    qryServicos: TFDQuery;
    qryCadastrarServico: TFDQuery;
    qryUpdateServico: TFDQuery;
    dsServicos: TDataSource;
    dsClientes: TDataSource;
    qryCadastrarCliente: TFDQuery;
    qryAlterarCliente: TFDQuery;
    qryVerificarCPF: TFDQuery;
    qryDadosAlterarCliente: TFDQuery;
    qryBuscaCliente: TFDQuery;
    dsBuscaCliente: TDataSource;
    qryBuscaClienteCodigo: TFDQuery;
    qryBuscaServico: TFDQuery;
    qryBuscaServicoCodigo: TFDQuery;
    dsBuscaServico: TDataSource;
    qryBuscaCardAtivo: TFDQuery;
    qryInserirMovimentacao: TFDQuery;
    qryAtualizaSaldo: TFDQuery;
    qryCriarCard: TFDQuery;
    qryInativarCardCliente: TFDQuery;
    procedure DataModuleCreate(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
    procedure Conectar;
    procedure AbrirCardClientes(const Pesquisa: string);
    procedure InativarCardCliente(CodigoCard: Integer);
    procedure AbrirServicos;
    procedure CadastrarServico(const Nome, Descricao: string);
    procedure AtualizarServico(ACodigo: Integer; const ANome, ADescricao: string);
    procedure AbrirClientes(const Pesquisa: string);
    procedure CadastrarCliente(
      const Nome, CPF, Telefone: string;
      const DtNasc: TDate;
      const Sexo: Char;
      const CEP: string;
      const Logradouro, Numero, Complemento, Bairro, Cidade, Estado: string
    );
    procedure AlterarCliente(
      Codigo: Integer;
      const Nome: string;
      const Telefone: string;
      const DtNasc: TDate;
      const Sexo: Char;
      const CEP: string;
      const Logradouro: string;
      const Numero: string;
      const Complemento: string;
      const Bairro: string;
      const Cidade: string;
      const Estado: string
    );

    function CPFExiste(const CPF: string): Boolean;
    function BuscaClienteCodigo(Codigo: Integer; out Nome: string): Boolean;
    function BuscaServicoCodigo(Codigo: Integer; out Nome: string): Boolean;
    function ObterOuCriarCard(
      Cliente: Integer;
      ValorInicial: Currency;
      out CodigoCard: Integer;
      out SaldoAtual: Currency
    ): Boolean;
  end;

var
  DataModule1: TDataModule1;

implementation

{%CLASSGROUP 'Vcl.Controls.TControl'}

{$R *.dfm}
function TDataModule1.CPFExiste(const CPF: string): Boolean;
begin
  qryVerificarCPF.Close;
  qryVerificarCPF.ParamByName('CPF').AsString := CPF;
  qryVerificarCPF.Open;

  if qryVerificarCPF.FieldByName('QTD').AsInteger > 0 then
    Result := True
  else
    Result := False;

end;

function TDataModule1.BuscaClienteCodigo(Codigo: Integer; out Nome: string): Boolean;
begin
  Result := False;
  Nome := '';

  qryBuscaClienteCodigo.Close;
  qryBuscaClienteCodigo.ParamByName('CODIGO').AsInteger := Codigo;
  qryBuscaClienteCodigo.Open;

  if not qryBuscaClienteCodigo.IsEmpty then
  begin
    Nome := qryBuscaClienteCodigo.FieldByName('NOME').AsString;
    Result := True;
  end;
end;

function TDataModule1.BuscaServicoCodigo(Codigo: Integer; out Nome: string): Boolean;
begin
  Result := False;
  Nome := '';

  qryBuscaServicoCodigo.Close;
  qryBuscaServicoCodigo.ParamByName('CODIGO').AsInteger := Codigo;
  qryBuscaServicoCodigo.Open;

  if not qryBuscaServicoCodigo.IsEmpty then
  begin
    Nome := qryBuscaServicoCodigo.FieldByName('NOME').AsString;
    Result := True;
  end;
end;

function TDataModule1.ObterOuCriarCard(
  Cliente: Integer;
  ValorInicial: Currency;
  out CodigoCard: Integer;
  out SaldoAtual: Currency
): Boolean;
begin
  Result := False;

  qryBuscaCardAtivo.Close;
  qryBuscaCardAtivo.ParamByName('CLIENTE').AsInteger := Cliente;
  qryBuscaCardAtivo.Open;

  if not qryBuscaCardAtivo.IsEmpty then
  begin
    CodigoCard := qryBuscaCardAtivo.FieldByName('CODIGO').AsInteger;
    SaldoAtual := qryBuscaCardAtivo.FieldByName('VALOR').AsCurrency;
    Result := True;
  end
  else
  begin
    qryCriarCard.Close;
    qryCriarCard.ParamByName('CLIENTE').AsInteger := Cliente;
    qryCriarCard.ParamByName('VALOR').AsCurrency := ValorInicial;
    qryCriarCard.ExecSQL;

    CodigoCard := FDConnection.GetLastAutoGenValue('');
    SaldoAtual := ValorInicial;
    Result := True;
  end;
end;

procedure TDataModule1.InativarCardCliente(CodigoCard: Integer);
begin
  qryInativarCardCliente.Close;
  qryInativarCardCliente.ParamByName('CODIGO').AsInteger := CodigoCard;
  qryInativarCardCliente.ExecSQL;
end;

procedure TDataModule1.CadastrarCliente(
  const Nome: string; const CPF: string; const Telefone: string;
  const DtNasc: TDate; const Sexo: Char; const CEP: string;
  const Logradouro: string; const Numero: string; const Complemento: string;
  const Bairro: string; const Cidade: string; const Estado: string);
begin
  qryCadastrarCliente.Close;

  qryCadastrarCliente.ParamByName('NOME').AsString := Nome;
  qryCadastrarCliente.ParamByName('CPF').AsString := CPF;
  qryCadastrarCliente.ParamByName('TELEFONE').AsString := Telefone;
  qryCadastrarCliente.ParamByName('DNASC').AsDate := DtNasc;
  qryCadastrarCliente.ParamByName('SEXO').AsString := Sexo;
  qryCadastrarCliente.ParamByName('CEP').AsString := CEP;
  qryCadastrarCliente.ParamByName('LOGRADOURO').AsString := Logradouro;
  qryCadastrarCliente.ParamByName('NUMERO').AsString := Numero;
  qryCadastrarCliente.ParamByName('COMPLEMENTO').AsString := Complemento;
  qryCadastrarCliente.ParamByName('BAIRRO').AsString := Bairro;
  qryCadastrarCliente.ParamByName('CIDADE').AsString := Cidade;
  qryCadastrarCliente.ParamByName('ESTADO').AsString := Estado;

  qryCadastrarCliente.ExecSQL;
end;

procedure TDataModule1.AlterarCliente(
  Codigo: Integer; const Nome: string; const Telefone: string;
  const DtNasc: TDate; const Sexo: Char; const CEP: string; const
  Logradouro: string; const Numero: string; const Complemento: string;
  const Bairro: string; const Cidade: string; const Estado: string);
begin
  qryAlterarCliente.Close;

  qryAlterarCliente.ParamByName('CODIGO').AsInteger := Codigo;
  qryAlterarCliente.ParamByName('NOME').AsString := Nome;
  qryAlterarCliente.ParamByName('TELEFONE').AsString := Telefone;
  qryAlterarCliente.ParamByName('DNASC').AsDate := DtNasc;
  qryAlterarCliente.ParamByName('SEXO').AsString := Sexo;
  qryAlterarCliente.ParamByName('CEP').AsString := CEP;
  qryAlterarCliente.ParamByName('LOGRADOURO').AsString := Logradouro;
  qryAlterarCliente.ParamByName('NUMERO').AsString := Numero;
  qryAlterarCliente.ParamByName('COMPLEMENTO').AsString := Complemento;
  qryAlterarCliente.ParamByName('BAIRRO').AsString := Bairro;
  qryAlterarCliente.ParamByName('CIDADE').AsString := Cidade;
  qryAlterarCliente.ParamByName('ESTADO').AsString := Estado;

  qryAlterarCliente.ExecSQL;
end;

procedure TDataModule1.CadastrarServico(const Nome, Descricao: string);
begin
  qryCadastrarServico.Close;
  qryCadastrarServico.ParamByName('NOME').AsString := Nome;
  qryCadastrarServico.ParamByName('DESCRICAO').AsString := Descricao;
  qryCadastrarServico.ExecSQL;
end;

procedure TDataModule1.AtualizarServico(ACodigo: Integer; const ANome: string; const ADescricao: string);
begin
  qryUpdateServico.Close;
  qryUpdateServico.ParamByName('CODIGO').AsInteger := ACodigo;
  qryUpdateServico.ParamByName('NOME').AsString := ANome;
  qryUpdateServico.ParamByName('DESCRICAO').AsString := ADescricao;
  qryUpdateServico.ExecSQL;
end;


procedure TDataModule1.Conectar;
begin
  try
    FDConnection.Connected := False;
    FDConnection.Params.Clear;

    FDConnection.Params.Add('DriverID=MySQL');
    FDConnection.Params.Add('Server=127.0.0.1');
    FDConnection.Params.Add('Port=3306');
    FDConnection.Params.Add('Database=delphi_ajx');
    FDConnection.Params.Add('User_Name=root');
    FDConnection.Params.Add('Password=SQLr00t!');

    FDConnection.LoginPrompt := False;
    FDConnection.Connected := True;
  except
    on E: Exception do
      raise Exception.Create(
        'Erro ao conectar ao banco de dados:' + sLineBreak + E.Message
      );
  end;
end;

procedure TDataModule1.AbrirCardClientes(const Pesquisa: string);
begin
  qryCardClientes.Close;

  if Trim(Pesquisa) = '' then
    qryCardClientes.ParamByName('PESQUISA').AsString := '%'
  else
    qryCardClientes.ParamByName('PESQUISA').AsString :=
      '%' + Pesquisa + '%';

  qryCardClientes.Open;
end;

procedure TDataModule1.AbrirServicos;
begin
  qryServicos.Close;
  qryServicos.Open;
end;

procedure TDataModule1.AbrirClientes(const Pesquisa: string);
begin
  qryClientes.Close;

  if Pesquisa.Trim = '' then
    qryClientes.ParamByName('PESQUISA').AsString := '%'
  else
    qryClientes.ParamByName('PESQUISA').AsString := '%' + Pesquisa + '%';

  qryClientes.Open;
end;

procedure TDataModule1.DataModuleCreate(Sender: TObject);
begin
  Conectar;
end;

end.
